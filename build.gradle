buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "net.serenity-bdd:serenity-gradle-plugin:$serenityVersion"
  }
}

apply plugin: 'java'

apply plugin: 'eclipse'
eclipse.classpath {
  defaultOutputDir = file('classes')
  file.whenMerged { classpath ->
    classpath.entries.findAll { 
      it.kind == 'src' && it.output.startsWith('bin/') 
    }.each {
      it.output = it.output.replace('bin/', 'classes/')
    }
  }
}

apply plugin: 'net.serenity-bdd.aggregator'
check.dependsOn checkOutcomes
build.dependsOn aggregate

task deleteTargetDir(type: Delete) {
  delete file('target')
}

task moveSerenityReportsToBuildDir(type: Copy) {
  from file('target/site')
  into reportsDir
  mustRunAfter checkOutcomes
  finalizedBy deleteTargetDir
}

aggregate.finalizedBy moveSerenityReportsToBuildDir

repositories {
  jcenter()
}

dependencies {
  compile "org.jacoco:org.jacoco.core:$jacocoVersion",
    "org.junit.platform:junit-platform-launcher:$junit5Version",
    "org.junit.vintage:junit-vintage-engine:$junit4Version",
    'org.yaml:snakeyaml:1.19'
  testCompile "net.serenity-bdd:serenity-screenplay:$serenityVersion",
      "net.serenity-bdd:serenity-junit:$serenityVersion"
}


defaultTasks 'build'

task wrapper(type: Wrapper) {
  gradleVersion = gradleWrapperVersion
}
