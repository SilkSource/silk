buildscript {
	repositories {
	  jcenter()
	}
  dependencies {
    classpath 'org.yaml:snakeyaml:1.15'
  }
}

import org.yaml.snakeyaml.Yaml

plugins {
 id 'java'
 id 'eclipse'
}

eclipse.classpath.defaultOutputDir = file('classes')

defaultTasks 'build'

task wrapper(type: Wrapper) {
  gradleVersion = '2.12'
}

repositories {
  jcenter()
}
dependencies {
  testCompile 'net.serenity-bdd:serenity-jbehave:1.6.0',
      'net.serenity-bdd:serenity-core:1.1.28'
}

task checkGlossary << {
  validateYaml fileTree(dir: 'src/main/glossary')  
}

def validateYaml(files) {
  def yaml = new Yaml()
  files.include '**/*.yml'
  files.each { file ->
    file.withInputStream { stream ->
      def map = yaml.load(stream)
      if (!(map instanceof Map)) {
        throw new GradleException("Invalid YAML file: $file.path")
      }
    }
  }
}

task checkGoals << {
  validateYaml fileTree(dir: 'src/main/goals')  
}

check.dependsOn 'checkGlossary', 'checkGoals'

task cleanUpAfterTest(type: Delete) {
  delete file('target')
}

test {
  finalizedBy cleanUpAfterTest
  systemProperty 'serenity.outputDirectory', new File(buildDir, 'jbehave').path
}
