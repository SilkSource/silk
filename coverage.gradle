apply plugin: 'jacoco'

jacoco {
  toolVersion = jacocoVersion
}

def excludeSpikesFrom(files) {
  project.files(files.collect { dir ->
    fileTree(dir) {
      exclude '**/spike/*'
    }
  })
}

jacocoTestCoverageVerification {
  afterEvaluate {
    classDirectories = excludeSpikesFrom(classDirectories)
    sourceDirectories = excludeSpikesFrom(sourceDirectories)
  }
  if (!project.name.startsWith('internal') && !project.hasProperty('beta')) {
    violationRules {
      rule {
        limit {
          counter = 'LINE'
              minimum = 0.8
        }
        limit {
          counter = 'COMPLEXITY'
              minimum = 0.8
        }
        limit {
          counter = 'BRANCH'
              minimum = 0.5
        }
      }
    }
  }
}

jacocoTestReport {
  reports {
    xml.enabled true
  }
  afterEvaluate {
    classDirectories = excludeSpikesFrom(classDirectories)
    sourceDirectories = excludeSpikesFrom(sourceDirectories)
  }
  finalizedBy jacocoTestCoverageVerification
}

test {
  finalizedBy jacocoTestReport
}
